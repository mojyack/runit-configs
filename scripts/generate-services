#!/bin/zsh

set -e

source ./config
build="/tmp/generate-service-$$"
svdir=$build/services

if [[ ! -e $svroot ]]; then
    firstboot=1
else
    firstboot=0
fi

rm -rf "$build"

# base svdir
mkdir -p "$svdir"
for service in $enabled_services; do
    src=services/$service
    dst=$svdir/$service/

    if [[ -e $src/oneshot && $firstboot != 1 ]]; then
        continue
    fi

    cp -r "$src" "$dst"

    if [[ -e $src/oneshot ]]; then
        run=$dst/run
        echo -e "\nexec rm -r \"\$PWD\"" >> $run
    fi

    if [[ -e $src/autolog ]]; then
        run=$dst/log/run
        log=$logdir/$service

        mkdir -p "$log"
        mkdir -p "$dst/log"
        echo "#!/bin/sh" > $run
        echo "exec svlogd -t $log" >> $run
        chmod u+x "$run"
    fi
done

# ttys
for n in $ttys; do
    dst=$svdir/getty-tty$n
    run=$dst/run

    # autologin
    autologin_user=autologin_tty$n
    autologin_user=${(P)autologin_user}
    if [[ -n $autologin_user ]]; then
        autologin_args="--autologin '$autologin_user'"
    else
        autologin_args=""
    fi

    mkdir -p $dst
    echo "#!/bin/sh" > $run
    echo "exec chpst -P /sbin/agetty $autologin_args tty$n linux" >> $run
    chmod u+x $run
done

for f in $install_files; do
    ln -s "$(realpath $f)" "$build/$f"
done

# install
mkdir -p "$svroot"
rsync -r --links "$build/" "$svroot"
rm -r "$build"
